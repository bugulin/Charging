import org.yaml.snakeyaml.Yaml

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp' version '1.8.0-1.0.9'
    id 'io.gitlab.arturbosch.detekt'
}

def keystoreConfigFile = rootProject.file("keystore.yml")
def keystoreConfig = new Yaml().load(keystoreConfigFile.newInputStream())

android {
    signingConfigs {
        release {
            storeFile file(keystoreConfig['storeFile'])
            storePassword keystoreConfig['storePassword']
            keyAlias keystoreConfig['keyAlias']
            keyPassword keystoreConfig['keyPassword']
        }
    }
    namespace 'cz.bornasp.charging'
    compileSdk 33

    defaultConfig {
        applicationId "cz.bornasp.charging"
        minSdk 26
        targetSdk 33
        versionCode 3
        versionName "1.2"

        resourceConfigurations += ["en", "cs"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.compose.material3:material3:1.0.1'
    implementation "androidx.compose.material3:material3-window-size-class:1.0.1"
    implementation "androidx.compose.material:material-icons-extended:1.3.1"
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "androidx.navigation:navigation-compose:2.5.3"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Testing
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    // Detekt
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"
}

detekt {
    toolVersion = "$detekt_version"
    config = files("$rootDir/config/detekt/detekt.yml")
    buildUponDefaultConfig = true
    basePath = projectDir
}
